clear stack

define OnsPlosive [ b -> p, d -> t, g -> k, p -> pʰ, t -> tʰ, k -> kʰ || [ .#. | [1|2|3|4|5] ] _ ] ;
define Digraphs [ [ z h ] -> %+zh, [ c h ] -> %+ch, [ s h ] -> %+sh, [ n g ] -> %+ng ] ;
define OnsAffricate [ z -> t͡s, %+zh -> t͡ʂ, j -> t͡ɕ, c -> t͡sʰ, %+ch -> t͡ʂʰ, q -> t͡ɕʰ ] ;
define OnsFricative [ f -> f, s -> s, %+sh -> ʂ, x -> ɕ, h -> x ] ;
define OnsGlideFront1 [ y -> ɥ || _ u ] ;
define OnsGlideFront2 [ y -> j ] ;
define OnsLiquid [ r -> ɻ ] ;
define Onsets Digraphs .o. OnsPlosive .o. OnsAffricate .o. OnsFricative .o. OnsGlideFront1 .o. OnsGlideFront2 .o. OnsLiquid ;

define RimeRhotics [ e r -> a ɻ || _ [1|2|3|4|5] ] ;
define SpecialDiphthongs [ [ i u ] -> [ i o u ], [ a o ] -> [ a u ] ] ;
define PalatalLabialGlide [ u -> v || [j|q|x|y] _ ] ;
define MedialsToGlides [ i -> j, u -> w, y -> ɥ || _ [i|e|a|o|u] ] ;
define DelOnsGlide [ j -> 0 || _ i, ɥ -> 0 || _ y, w -> 0 || _ u ] ;
define Nuclei [ a -> a, e -> ə, i -> i, o -> o, u -> u, v -> y ] ;
define Codas [ i -> j, u -> w, %+ng -> ŋ || _ [ .#. | [1|2|3|4|5] ] ] ;
define Rimes RimeRhotics .o. SpecialDiphthongs .o. PalatalLabialGlide .o. DelOnsGlide .o. MedialsToGlides .o. Nuclei .o. Codas ;

define Tones [ [1|2|3|4|5] -> 0 || .#. ] .o. [ [1|2|3|4|5] -> %- ] ;

! define Pinyin Onsets .o. Rimes .o. Tones ;

! push Pinyin

! load stack cedict-pinyin-foma.fst

! compose net

